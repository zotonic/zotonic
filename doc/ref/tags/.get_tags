#!/usr/bin/sed -f

# operate on the first lines of the identifier_to_keyword fun
# that mentions open_tag (guard clause), and down until the
# the line containing "Type ="
/identifier_to_keyword.*open_tag/,/Type =/ {
    # delete lines that doesn't have any " in them
    /^[^"]*$/ d

    # delete everyting upto the next quoted word, and the quotes
    # leave a space in place
    s/[^"]*"\([^"]*\)"/\1 /g

    # strip out spurious erlang syntax scrap
    s/[],]//g

    # get rid of all close tags (those prefixed with 'end')
    s/end[a-z]*//g

    # print whats left
    p
}

# delete everything else
d
